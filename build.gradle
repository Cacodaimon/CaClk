buildscript {
    ext.kotlin_version = '1.1.4-3'
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.moowork.gradle:gradle-node-plugin:1.2.0"
    }
}

apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: 'idea'
apply plugin: 'com.moowork.node'

mainClassName = 'de.cacodaemon.caclk.server.MainKt'

defaultTasks 'run'

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.slf4j:slf4j-log4j12:1.7.22'
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile 'com.google.code.gson:gson:2.8.2'
    compile "com.sparkjava:spark-core:2.5.4"
    compile 'commons-io:commons-io:2.6'
    compile 'org.apache.httpcomponents:fluent-hc:4.5.3'
    compile 'com.squareup.okhttp3:okhttp:3.9.1'
    compile fileTree(dir: 'libs', include: '*.jar')

    testCompile 'junit:junit:4.11'
    testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
}

task wrapper(type: Wrapper) {
    gradleVersion = "4.1"
}

task webpackBuild(type: NpmTask) {
    args = ['run', 'build']
    execOverrides {
        it.workingDir = 'src/main/frontend/caclk'
    }
}

compileKotlin.dependsOn webpackBuild

jar {
    manifest {
        attributes 'Main-Class': mainClassName
    }

    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
}